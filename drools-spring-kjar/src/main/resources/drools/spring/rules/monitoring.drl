package drools.spring.rules;
dialect  "mvel"

import ftn.sbz.cdssserver.model.monitoring.MonitoringPatient;
import ftn.sbz.cdssserver.service.MonitoringService;
import ftn.sbz.cdssserver.model.monitoring.OxygenLevel;
import ftn.sbz.cdssserver.model.monitoring.HeartBeat;
import ftn.sbz.cdssserver.model.monitoring.Urination;

import java.util.List;
import org.springframework.messaging.simp.SimpMessagingTemplate;

//global SimpMessagingTemplate messagingTemplate;
global MonitoringService monitoringService;

rule "oxygen problems"
agenda-group "monitoring"
    when
        $monitoringPatient: MonitoringPatient(oxygenLevel != null, oxygenLevel.level < 70)
        // List(size == 0) from collect
        not ( OxygenLevel(patient == $monitoringPatient, growth == true) over window:time(15m) )
    then
        String message = "\nOxy problems for " + $monitoringPatient.getPatient().getName();
        System.out.println(message);
        monitoringService.sendMessage(message);
//        messagingTemplate.convertAndSend("/monitoring", message); // doesn't work in tests
end

rule "heart problems"
agenda-group "monitoring"
    when
        $monitoringPatient: MonitoringPatient()
        List(size > 25) from collect (
            HeartBeat(patient == $monitoringPatient) over window:time(10s)
        )
    then
        String message = "\nHeart problems for " + $monitoringPatient.getPatient().getName();
        System.out.println(message);
        monitoringService.sendMessage(message);
//        messagingTemplate.convertAndSend("/monitoring", message); // doesn't work in tests
end


rule "urgent dialysis"
agenda-group "monitoring"
    when
        $monitoringPatient: MonitoringPatient(sickness != null && sickness.name == "Chronic kidney disease")
        List(size > 10) from collect (
            HeartBeat(patient == $monitoringPatient) over window:time(10s)
        )
        Number(doubleValue < 100) from accumulate (
            Urination(patient == $monitoringPatient, $a: amount) over window:time(12h),
            sum($a)
        )
    then
        String message = "\nUrgent dialysis for " + $monitoringPatient.getPatient().getName();
        System.out.println(message);
        monitoringService.sendMessage(message);
//        messagingTemplate.convertAndSend("/monitoring", message); // doesn't work in tests
end