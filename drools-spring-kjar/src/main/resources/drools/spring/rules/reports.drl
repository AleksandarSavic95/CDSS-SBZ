package drools.spring.rules;
dialect  "mvel"

import ftn.sbz.cdssserver.model.Patient;
import ftn.sbz.cdssserver.model.Treatment;
import ftn.sbz.cdssserver.model.sickness.Sickness;
import ftn.sbz.cdssserver.model.medicine.Medicine;

import java.util.List;
import java.util.Set;
import java.util.HashSet;

/**
● Spisak pacijenata sa mogući hroničnim oboljenjima:
    o Pacijent je u poslednje dve godine više od 5 puta lečen od iste bolesti
    o Bolest nije prehlada ili groznica
**/
query chronicallySickPatients()
    $p: Patient() over window:time(5s)
    $s: Sickness($name: name) over window:time(10s)
    List(size > 5) from collect (
        Treatment(patient == $p, sickness == $s, sickness.name != "Cold", sickness.name != "Fever")
        over window:time(730)
    )
end


/**
● Spisak mogućih zavisnika:
    o Pacijentu su u poslednjih 6 meseci barem 6 puta prepisani analgetici od strane 3 ili više različita
lekara
**/
query addicts()
    $p: Patient() over window:time(5s)
    $treatments: List(size >= 6) from collect (
        Treatment(patient == $p, this.hasAnalgetics()) over window:time(180d)
    )
    // three different doctors prescribed analgetics
    $d: List(size >= 3) from accumulate (
        Treatment($doctor: doctor) from $treatments,
        init(Set doctorsSet = new HashSet();),
        action(doctorsSet.add($doctor);),
        result(doctorsSet)
    )
end
/**
● Spisak pacijenta sa oslabljenim imunitetom:
    o Pacijent je lečen više od 10 puta u poslednjih 12 meseci od barem 2 različite bolesti za koje su mu
prepisani antibiotici
    o Pacijent u poslednjih 12 meseci nije bolovao od bolesti za koju mu nisu bili prepisani antibiotici
    ooo TJ prepisani su mu antibiotici za sve bolesti od kojih je bolovao u posljednjih 12 mjeseci
**/
query weakImmunityPatients()
    $p: Patient() over window:time(5s)
    $antibioticsTreatments: List(size > 10) from collect (
        $t: Treatment(patient == $p, this.hasAntibiotics()) over window:time(365d)
    )
    $sicknessesWithAntibiotics: Set(size >= 2) from accumulate(
        Treatment($sickness: sickness) from $antibioticsTreatments,
        init(Set sicknessSet = new HashSet();),
        action(sicknessSet.add($sickness);),
        result(sicknessSet)
    )
    not (
        Treatment(patient == $p, this.hasAntibiotics() == false) over window:time(365d)
    )
end